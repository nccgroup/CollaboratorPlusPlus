plugins {
    id 'java'
}

group 'nccgroup'
version '0.2-Beta'
sourceCompatibility = 1.8


repositories {
    mavenCentral()
    maven {
        url "http://repo.spring.io/plugins-release/"
    }
    maven {
        url "https://jitpack.io"
    }
}

configurations {
    includeUnexplodedLib
    compile.extendsFrom includeUnexplodedLib
}

dependencies {
    compile 'com.github.CoreyD97:BurpExtenderUtilities:890266f248916ad535a1d1d1813d1d7999b75706'
    compile 'net.portswigger.burp.extender:burp-extender-api:1.7.22'
    compile 'com.google.code.gson:gson:+'
    compile group: 'org.apache.httpcomponents', name: 'httpclient', version: '4.5.6'
    compile group: 'commons-io', name: 'commons-io', version: '2.4'
    compile group: 'nu.studer', name: 'java-ordered-properties', version: '1.0.1'
    testCompile files('/opt/BurpSuitePro/burpsuite_pro.jar')
    includeUnexplodedLib group: 'org.bouncycastle', name: 'bcprov-jdk15on', version: '1.61'
}

tasks.withType(Jar){
    destinationDir = file("$rootDir/releases")
}

task clientJar(type: Jar){
    baseName "CollaboratorAuth-CLIENT"
    from{
        (configurations.runtime).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }{
        exclude "META-INF/*.SF"
        exclude "META-INF/*.DSA"
        exclude "META-INF/*.RSA"
    }
}

task serverJar(type: Jar){
    baseName "CollaboratorAuth-SERVER"
    manifest {
        attributes(
                'Main-Class': 'com.nccgroup.collaboratorauth.server.Launcher',
        )
    }

    from{
        (configurations.runtime - configurations.includeUnexplodedLib).collect {
            it.isDirectory() ? it : zipTree(it)
        }
    }

    into('lib'){
        from configurations.includeUnexplodedLib
    }
}

assemble.dependsOn(clientJar, serverJar)